import QtQuick
import QtQuick.Effects
import QtQuick.Layouts
import Qt.labs.folderlistmodel
import Quickshell
import Quickshell.Hyprland
import Quickshell.Io
import Quickshell.Services.Greetd
import qs.Common
import qs.Services
import qs.Widgets
import qs.DankCommon.Session
import "../../DankCommon/Common/LayoutCodes.js" as LayoutCodes

Item {
    id: root

    function encodeFileUrl(path) {
        if (!path)
            return "";
        return "file://" + path.split('/').map(s => encodeURIComponent(s)).join('/');
    }

    component ClockDigitText: StyledText {
        font.pixelSize: LockMetrics.clockSize
        font.weight: Theme.fontWeight
        color: Theme.lockScreenContentColor
        horizontalAlignment: Text.AlignHCenter
    }

    onPamStateChanged: {
        if (pamState !== "")
            errorShake.restart();
    }

    function cycleKeyboardLayout() {
        if (CompositorService.isNiri) {
            NiriService.cycleKeyboardLayout();
            return;
        }
        if (!CompositorService.isHyprland)
            return;
        Quickshell.execDetached(["hyprctl", "switchxkblayout", hyprlandKeyboard, "next"]);
        updateHyprlandLayout();
    }

    function desktopIdFromPath(path) {
        if (!path)
            return "";
        const parts = path.split("/");
        const id = parts.length > 0 ? parts[parts.length - 1] : path;
        return id || "";
    }

    readonly property string xdgDataDirs: Quickshell.env("XDG_DATA_DIRS")
    property string screenName: ""
    property string hyprlandCurrentLayout: ""
    property string hyprlandKeyboard: ""
    property int hyprlandLayoutCount: 0
    readonly property int keyboardLayoutCount: {
        if (CompositorService.isNiri)
            return NiriService.keyboardLayoutNames.length;
        if (CompositorService.isHyprland)
            return hyprlandLayoutCount;
        return 0;
    }
    readonly property string keyboardLayoutLabel: {
        if (CompositorService.isNiri)
            return LayoutCodes.layoutCode(NiriService.getCurrentKeyboardLayoutName());
        if (CompositorService.isHyprland)
            return hyprlandCurrentLayout;
        return "";
    }
    readonly property string pamState: GreeterState.pamState
    property bool isPrimaryScreen: !Quickshell.screens?.length || screenName === Quickshell.screens[0]?.name

    property bool weatherInitialized: false
    property bool awaitingExternalAuth: false
    property bool pendingPasswordResponse: false
    property bool passwordSubmitRequested: false
    property int defaultAuthTimeoutMs: 10000
    property int externalAuthTimeoutMs: 30000
    property int memoryFlushDelayMs: 120
    property string pendingLaunchCommand: ""
    property var pendingLaunchEnv: []
    property int passwordFailureCount: 0
    property int passwordAttemptLimitHint: 0
    property string authFeedbackMessage: ""
    // The literal prompt text PAM asked for (e.g. "PIN:", "Password:", "Verification code:"),
    // captured from Greetd.onAuthMessage. Falls back to a generic label when empty.
    property string authPromptText: ""
    property string greetdPamText: ""
    property string systemAuthPamText: ""
    property string commonAuthPamText: ""
    property string passwordAuthPamText: ""
    property string systemLoginPamText: ""
    property string systemLocalLoginPamText: ""
    property string commonAuthPcPamText: ""
    property string loginPamText: ""
    property string faillockConfigText: ""
    property string authSessionAutoStartedForUser: ""
    property bool fprintdProbeComplete: false
    property bool fprintdHasDevice: false
    property bool autoLoginOnSuccess: false
    readonly property bool greeterPamStackHasFprint: greeterPamStackHasModule("pam_fprintd")
    // Falls back to PAM-only detection until the fprintd D-Bus probe completes.
    readonly property bool greeterPamHasFprint: greeterPamStackHasFprint && (!fprintdProbeComplete || fprintdHasDevice)
    readonly property bool greeterPamHasU2f: greeterPamStackHasModule("pam_u2f")
    readonly property bool greeterPamHasHowdy: greeterPamStackHasModule("pam_howdy")
    readonly property bool greeterExternalAuthAvailable: (greeterPamHasFprint && SettingsData.greeterEnableFprint) || (greeterPamHasU2f && SettingsData.greeterEnableU2f) || greeterPamHasHowdy
    readonly property bool greeterPamHasExternalAuth: greeterPamHasFprint || greeterPamHasU2f || greeterPamHasHowdy
    readonly property bool externalAuthInProgress: awaitingExternalAuth || (Greetd.state !== GreetdState.Inactive && passwordSubmitRequested && greeterPamHasExternalAuth && !pendingPasswordResponse)
    readonly property string externalAuthStatusMessage: {
        if (!externalAuthInProgress)
            return "";
        if (greeterPamHasFprint && greeterPamHasU2f)
            return I18n.tr("Awaiting fingerprint or security key authentication");
        if (greeterPamHasFprint)
            return I18n.tr("Awaiting fingerprint authentication");
        if (greeterPamHasHowdy)
            return I18n.tr("Awaiting face authentication");
        return I18n.tr("Awaiting security key authentication");
    }
    readonly property string authDisplayMessage: authFeedbackMessage || externalAuthStatusMessage
    readonly property bool autoLoginAvailable: GreetdSettings.rememberLastUser && GreetdSettings.rememberLastSession
    readonly property bool multipleUsersAvailable: GreeterUsersService.loaded && GreeterUsersService.users.length > 1
    // Single-user systems get the picker too when auto-login is available, so the
    // auto-login toggle lives inside the dropdown instead of floating on its own.
    readonly property bool pickerAvailable: multipleUsersAvailable || (GreeterUsersService.loaded && GreeterUsersService.users.length === 1 && autoLoginAvailable)
    readonly property bool showUserPicker: pickerAvailable && !GreeterState.showPasswordInput && !manualUsernameEntry
    readonly property bool showAccountSwitchLink: pickerAvailable && manualUsernameEntry && !GreeterState.showPasswordInput && !GreeterState.unlocking
    readonly property int userPickerMaxHeight: Math.min(400, Math.max(120, height * 0.35))
    property bool userListOpen: false
    property bool manualUsernameEntry: false
    property bool skipAutoSelectUser: false
    property string pickerThemeUsername: ""

    function initWeatherService() {
        if (weatherInitialized)
            return;
        if (!GreetdSettings.settingsLoaded)
            return;
        if (!SettingsData.lockScreenShowWeather)
            return;
        weatherInitialized = true;
        WeatherService.addRef();
        WeatherService.forceRefresh();
    }

    function stripPamComment(line) {
        if (!line)
            return "";
        const trimmed = line.trim();
        if (!trimmed || trimmed.startsWith("#"))
            return "";
        const hashIdx = trimmed.indexOf("#");
        if (hashIdx >= 0)
            return trimmed.substring(0, hashIdx).trim();
        return trimmed;
    }

    function pamModuleEnabled(pamText, moduleName) {
        if (!pamText || !moduleName)
            return false;
        const lines = pamText.split(/\r?\n/);
        for (let i = 0; i < lines.length; i++) {
            const line = stripPamComment(lines[i]);
            if (!line)
                continue;
            if (line.includes(moduleName))
                return true;
        }
        return false;
    }

    function pamTextIncludesFile(pamText, filename) {
        if (!pamText || !filename)
            return false;
        const lines = pamText.split(/\r?\n/);
        for (let i = 0; i < lines.length; i++) {
            const line = stripPamComment(lines[i]);
            if (!line)
                continue;
            if (line.includes(filename) && (line.includes("include") || line.includes("substack") || line.startsWith("@include")))
                return true;
        }
        return false;
    }

    function greeterPamStackHasModule(moduleName) {
        if (pamModuleEnabled(greetdPamText, moduleName))
            return true;
        const includedPamStacks = [["system-auth", systemAuthPamText], ["common-auth", commonAuthPamText], ["password-auth", passwordAuthPamText], ["system-login", systemLoginPamText], ["system-local-login", systemLocalLoginPamText], ["common-auth-pc", commonAuthPcPamText], ["login", loginPamText]];
        for (let i = 0; i < includedPamStacks.length; i++) {
            const stack = includedPamStacks[i];
            if (pamTextIncludesFile(greetdPamText, stack[0]) && pamModuleEnabled(stack[1], moduleName))
                return true;
        }
        return false;
    }

    function usesPamLockoutPolicy(pamText) {
        if (!pamText)
            return false;
        const lines = pamText.split(/\r?\n/);
        for (let i = 0; i < lines.length; i++) {
            const line = stripPamComment(lines[i]);
            if (!line)
                continue;
            if (line.includes("pam_faillock.so") || line.includes("pam_tally2.so") || line.includes("pam_tally.so"))
                return true;
        }
        return false;
    }

    function parsePamLineDenyValue(pamText) {
        if (!pamText)
            return -1;
        const lines = pamText.split(/\r?\n/);
        for (let i = 0; i < lines.length; i++) {
            const line = stripPamComment(lines[i]);
            if (!line)
                continue;
            if (!line.includes("pam_faillock.so") && !line.includes("pam_tally2.so") && !line.includes("pam_tally.so"))
                continue;
            const denyMatch = line.match(/\bdeny\s*=\s*(\d+)\b/i);
            if (!denyMatch)
                continue;
            const parsed = parseInt(denyMatch[1], 10);
            if (!isNaN(parsed))
                return parsed;
        }
        return -1;
    }

    function parseFaillockDenyValue(configText) {
        if (!configText)
            return -1;
        const lines = configText.split(/\r?\n/);
        for (let i = 0; i < lines.length; i++) {
            const line = stripPamComment(lines[i]);
            if (!line)
                continue;
            const denyMatch = line.match(/^deny\s*=\s*(\d+)\s*$/i);
            if (!denyMatch)
                continue;
            const parsed = parseInt(denyMatch[1], 10);
            if (!isNaN(parsed))
                return parsed;
        }
        return -1;
    }

    function refreshPasswordAttemptPolicyHint() {
        const pamSources = [greetdPamText, systemAuthPamText, commonAuthPamText, passwordAuthPamText, systemLoginPamText, systemLocalLoginPamText, commonAuthPcPamText, loginPamText];
        let lockoutConfigured = false;
        let denyFromPam = -1;
        for (let i = 0; i < pamSources.length; i++) {
            const source = pamSources[i];
            if (!source)
                continue;
            if (usesPamLockoutPolicy(source))
                lockoutConfigured = true;
            const denyValue = parsePamLineDenyValue(source);
            if (denyValue >= 0 && (denyFromPam < 0 || denyValue < denyFromPam))
                denyFromPam = denyValue;
        }

        if (!lockoutConfigured) {
            passwordAttemptLimitHint = 0;
            return;
        }

        const denyFromConfig = parseFaillockDenyValue(faillockConfigText);
        if (denyFromConfig >= 0) {
            passwordAttemptLimitHint = denyFromConfig;
            return;
        }

        if (denyFromPam >= 0) {
            passwordAttemptLimitHint = denyFromPam;
            return;
        }

        // pam_faillock default deny value when no explicit config is set.
        passwordAttemptLimitHint = 3;
    }

    function isLikelyLockoutMessage(message) {
        const lower = (message || "").toLowerCase();
        return lower.includes("account is locked") || lower.includes("too many") || lower.includes("maximum number of");
    }

    function currentAuthMessage() {
        if (GreeterState.pamState === "error")
            return I18n.tr("Authentication error - try again");
        if (GreeterState.pamState === "max")
            return I18n.tr("Too many failed attempts - account may be locked");
        if (GreeterState.pamState === "fail") {
            if (passwordAttemptLimitHint > 0) {
                const attempt = Math.max(1, Math.min(passwordFailureCount, passwordAttemptLimitHint));
                const remaining = Math.max(passwordAttemptLimitHint - attempt, 0);
                if (remaining > 0) {
                    return I18n.tr("Authentication failed - attempt %1 of %2").arg(attempt).arg(passwordAttemptLimitHint);
                }
                return I18n.tr("Authentication failed - lockout can occur");
            }
            return I18n.tr("Authentication failed - try again");
        }
        return "";
    }

    function clearAuthFeedback() {
        GreeterState.pamState = "";
        authFeedbackMessage = "";
        authPromptText = "";
    }

    Connections {
        target: GreetdSettings
        function onSettingsLoadedChanged() {
            if (GreetdSettings.settingsLoaded) {
                initWeatherService();
                if (isPrimaryScreen) {
                    applyLastSuccessfulUser();
                    finalizeSessionSelection();
                }
            }
        }

        function onRememberLastUserChanged() {
            if (!isPrimaryScreen)
                return;
            if (!GreetdSettings.rememberLastUser && GreetdMemory.lastSuccessfulUser) {
                GreetdMemory.setLastSuccessfulUser("");
            }
            applyLastSuccessfulUser();
        }

        function onRememberLastSessionChanged() {
            if (!isPrimaryScreen)
                return;
            if (!GreetdSettings.rememberLastSession && (GreetdMemory.lastSessionId || GreetdMemory.lastSessionDesktopId)) {
                GreetdMemory.setLastSession("", "");
            }
            finalizeSessionSelection();
        }
    }

    FileView {
        id: greetdPamWatcher
        path: "/etc/pam.d/greetd"
        printErrors: false
        onLoaded: {
            root.greetdPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.greetdPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: systemAuthPamWatcher
        path: "/etc/pam.d/system-auth"
        printErrors: false
        onLoaded: {
            root.systemAuthPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.systemAuthPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: commonAuthPamWatcher
        path: "/etc/pam.d/common-auth"
        printErrors: false
        onLoaded: {
            root.commonAuthPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.commonAuthPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: passwordAuthPamWatcher
        path: "/etc/pam.d/password-auth"
        printErrors: false
        onLoaded: {
            root.passwordAuthPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.passwordAuthPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: systemLoginPamWatcher
        path: "/etc/pam.d/system-login"
        printErrors: false
        onLoaded: {
            root.systemLoginPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.systemLoginPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: systemLocalLoginPamWatcher
        path: "/etc/pam.d/system-local-login"
        printErrors: false
        onLoaded: {
            root.systemLocalLoginPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.systemLocalLoginPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: commonAuthPcPamWatcher
        path: "/etc/pam.d/common-auth-pc"
        printErrors: false
        onLoaded: {
            root.commonAuthPcPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.commonAuthPcPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: loginPamWatcher
        path: "/etc/pam.d/login"
        printErrors: false
        onLoaded: {
            root.loginPamText = text();
            root.refreshPasswordAttemptPolicyHint();
            root.maybeAutoStartAuthSession();
        }
        onLoadFailed: {
            root.loginPamText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    FileView {
        id: faillockConfigWatcher
        path: "/etc/security/faillock.conf"
        printErrors: false
        onLoaded: {
            root.faillockConfigText = text();
            root.refreshPasswordAttemptPolicyHint();
        }
        onLoadFailed: {
            root.faillockConfigText = "";
            root.refreshPasswordAttemptPolicyHint();
        }
    }

    Component.onCompleted: {
        initWeatherService();
        refreshPasswordAttemptPolicyHint();

        if (isPrimaryScreen)
            applyLastSuccessfulUser();

        if (CompositorService.isHyprland)
            updateHyprlandLayout();

        fprintdDeviceProbe.running = true;
    }

    function applyPickerPreviewTheme() {
        let previewUser = (pickerThemeUsername || "").trim();
        if (!previewUser && GreetdSettings.rememberLastUser)
            previewUser = (GreetdMemory.lastSuccessfulUser || "").trim();
        if (previewUser)
            GreeterUserTheme.applyForUser(previewUser);
        else
            GreeterUserTheme.applyDefault();
    }

    function applyLastSuccessfulUser() {
        if (root.skipAutoSelectUser)
            return;
        if (!GreetdSettings.settingsLoaded || !GreetdSettings.rememberLastUser)
            return;
        const lastUser = GreetdMemory.lastSuccessfulUser;
        if (lastUser && !GreeterState.showPasswordInput && !GreeterState.username) {
            selectUser(lastUser);
        }
    }

    function enterManualUsernameEntry() {
        if (!root.pickerAvailable || GreeterState.showPasswordInput)
            return;
        root.manualUsernameEntry = true;
        root.userListOpen = false;
        GreeterState.username = "";
        GreeterState.usernameInput = "";
        inputField.text = "";
        root.applyPickerPreviewTheme();
        Qt.callLater(() => inputField.forceActiveFocus());
    }

    function returnToUserListFromManualEntry() {
        if (!root.pickerAvailable)
            return;
        root.manualUsernameEntry = false;
        root.userListOpen = true;
        GreeterState.username = "";
        GreeterState.usernameInput = "";
        inputField.text = "";
        root.applyPickerPreviewTheme();
    }

    function returnToUserPicker() {
        if (!root.pickerAvailable || GreeterState.unlocking)
            return;
        root.manualUsernameEntry = false;
        root.skipAutoSelectUser = true;
        awaitingExternalAuth = false;
        pendingPasswordResponse = false;
        passwordSubmitRequested = false;
        authTimeout.interval = defaultAuthTimeoutMs;
        authTimeout.stop();
        clearAuthFeedback();
        passwordFailureCount = 0;
        authSessionAutoStartedForUser = "";
        if (Greetd.state !== GreetdState.Inactive)
            Greetd.cancelSession();
        const previousUser = GreeterState.username;
        GreeterState.reset();
        inputField.text = "";
        if (previousUser)
            root.pickerThemeUsername = previousUser;
        root.applyPickerPreviewTheme();
        root.userListOpen = true;
    }

    function selectUser(rawValue) {
        const user = (rawValue || "").trim();
        if (!user)
            return;
        root.manualUsernameEntry = false;
        root.skipAutoSelectUser = false;
        submitUsername(user);
    }

    function submitUsername(rawValue) {
        const user = (rawValue || "").trim();
        if (!user)
            return;
        if (GreeterState.username !== user) {
            passwordFailureCount = 0;
            clearAuthFeedback();
            authSessionAutoStartedForUser = "";
        }
        root.pickerThemeUsername = user;
        GreeterState.username = user;
        GreeterState.usernameInput = user;
        GreeterState.showPasswordInput = true;
        root.userListOpen = false;
        GreeterState.passwordBuffer = "";
        pendingPasswordResponse = false;
        passwordSubmitRequested = false;
        maybeAutoStartAuthSession();
    }

    function submitBufferedPassword() {
        pendingPasswordResponse = false;
        passwordSubmitRequested = false;
        awaitingExternalAuth = false;
        authTimeout.interval = defaultAuthTimeoutMs;
        authTimeout.restart();
        // Some PAM stacks expect an explicit empty response to advance U2F/fprint or fail normally.
        Greetd.respond(GreeterState.passwordBuffer || "");
        GreeterState.passwordBuffer = "";
        inputField.text = "";
        return true;
    }

    function startAuthSession(submitPassword) {
        submitPassword = submitPassword === true;
        if (!GreeterState.showPasswordInput || !GreeterState.username)
            return;
        if (GreeterState.unlocking)
            return;
        const hasPasswordBuffer = GreeterState.passwordBuffer && GreeterState.passwordBuffer.length > 0;
        if (Greetd.state !== GreetdState.Inactive) {
            if (pendingPasswordResponse && submitPassword)
                submitBufferedPassword();
            else if (submitPassword)
                passwordSubmitRequested = true;
            return;
        }
        pendingPasswordResponse = false;
        passwordSubmitRequested = submitPassword;
        awaitingExternalAuth = !submitPassword && !hasPasswordBuffer && root.greeterExternalAuthAvailable;
        // Let the effective PAM stack finish external authentication.
        const waitingOnPamExternalBeforePassword = submitPassword && root.greeterPamHasExternalAuth;
        // With nothing submitted yet we are waiting for the user, not for PAM, so
        // no timeout applies - arming one here would drop the session (and the
        // prompt) after a few idle seconds. onAuthMessage and
        // submitBufferedPassword arm it once a response is actually in flight.
        const waitingForUserInput = !submitPassword && !hasPasswordBuffer && !awaitingExternalAuth;
        if (waitingForUserInput) {
            authTimeout.stop();
        } else {
            authTimeout.interval = (awaitingExternalAuth || waitingOnPamExternalBeforePassword) ? externalAuthTimeoutMs : defaultAuthTimeoutMs;
            authTimeout.restart();
        }
        Greetd.createSession(GreeterState.username);
    }

    // Start the PAM conversation as soon as a user is known, without waiting for
    // input. External auth (fprint/u2f/howdy) needs this to begin scanning, and
    // password/PIN stacks need it so the real prompt text is known before the
    // user types - otherwise the field can only ever show a generic label.
    function maybeAutoStartAuthSession() {
        if (!GreeterState.showPasswordInput || !GreeterState.username)
            return;
        if (GreeterState.unlocking || Greetd.state !== GreetdState.Inactive)
            return;
        if (passwordSubmitRequested)
            return;
        if (GreeterState.passwordBuffer && GreeterState.passwordBuffer.length > 0)
            return;
        if (authSessionAutoStartedForUser === GreeterState.username)
            return;

        authSessionAutoStartedForUser = GreeterState.username;
        startAuthSession(false);
    }

    Component.onDestruction: {
        if (weatherInitialized)
            WeatherService.removeRef();
    }

    function updateHyprlandLayout() {
        if (CompositorService.isHyprland) {
            hyprlandLayoutProcess.running = true;
        }
    }

    Process {
        id: greeterAutoLoginPendingProcess
        command: ["sh", "-c", "mkdir -p $(dirname " + JSON.stringify((Quickshell.env("DMS_GREET_CFG_DIR") || "/var/cache/dms-greeter") + "/.local/state/auto-login-sync-pending") + ") && touch " + JSON.stringify((Quickshell.env("DMS_GREET_CFG_DIR") || "/var/cache/dms-greeter") + "/.local/state/auto-login-sync-pending")]
        running: false
    }

    Process {
        id: hyprlandLayoutProcess
        running: false
        command: ["hyprctl", "-j", "devices"]
        stdout: StdioCollector {
            onStreamFinished: {
                try {
                    const data = JSON.parse(text);
                    const mainKeyboard = data.keyboards.find(kb => kb.main === true);
                    if (!mainKeyboard) {
                        hyprlandCurrentLayout = "";
                        hyprlandLayoutCount = 0;
                        return;
                    }
                    hyprlandKeyboard = mainKeyboard.name;
                    if (mainKeyboard.active_keymap) {
                        hyprlandCurrentLayout = LayoutCodes.layoutCode(mainKeyboard.active_keymap);
                    } else {
                        hyprlandCurrentLayout = "";
                    }
                    hyprlandLayoutCount = mainKeyboard.layout ? mainKeyboard.layout.split(",").length : 0;
                } catch (e) {
                    hyprlandCurrentLayout = "";
                    hyprlandLayoutCount = 0;
                }
            }
        }
    }

    // Probe fprintd D-Bus for physically enrolled scanners to eliminate PAM stack false-positives.
    Process {
        id: fprintdDeviceProbe
        running: false
        // sh wrapper: emits PROBE_UNAVAILABLE if gdbus is absent or fprintd unreachable,
        // keeping the PAM-only fallback active in those cases.
        command: ["sh", "-c", "command -v gdbus >/dev/null 2>&1 || { echo PROBE_UNAVAILABLE; exit 0; }; " + "gdbus call --system " + "--dest net.reactivated.Fprint " + "--object-path /net/reactivated/Fprint/Manager " + "--method net.reactivated.Fprint.Manager.GetDevices 2>/dev/null " + "|| echo PROBE_UNAVAILABLE"]
        stdout: StdioCollector {
            onStreamFinished: {
                if (text.includes("PROBE_UNAVAILABLE"))
                    return; // PAM-only fallback stays active
                root.fprintdHasDevice = text.includes("objectpath");
                root.fprintdProbeComplete = true;
                root.maybeAutoStartAuthSession();
            }
        }
        onExited: function (exitCode, exitStatus) {
            if (!root.fprintdProbeComplete)
                root.maybeAutoStartAuthSession(); // PAM-only fallback stays active
        }
    }

    Connections {
        target: CompositorService.isHyprland ? Hyprland : null
        enabled: CompositorService.isHyprland

        function onRawEvent(event) {
            if (event.name === "activelayout")
                updateHyprlandLayout();
        }
    }

    Connections {
        target: GreetdMemory
        enabled: isPrimaryScreen
        function onLastSuccessfulUserChanged() {
            applyLastSuccessfulUser();
        }
        function onMemoryReadyChanged() {
            finalizeSessionSelection();
        }
    }

    Connections {
        target: GreeterUsersService
        function onLoadedChanged() {
            if (GreeterUsersService.loaded && isPrimaryScreen)
                applyPickerPreviewTheme();
        }
        function onSyncedThemePathsChanged() {
            if (!isPrimaryScreen)
                return;
            if (GreeterState.username)
                GreeterUserTheme.applyForUser(GreeterState.username);
            else if (root.showUserPicker || root.userListOpen)
                applyPickerPreviewTheme();
        }
    }

    Connections {
        target: GreeterState
        function onUsernameChanged() {
            if (GreeterState.username) {
                root.pickerThemeUsername = GreeterState.username;
                GreeterUserTheme.applyForUser(GreeterState.username);
            } else if (root.showUserPicker || root.userListOpen) {
                applyPickerPreviewTheme();
            }
        }
        function onShowPasswordInputChanged() {
            if (GreeterState.showPasswordInput)
                root.userListOpen = false;
        }
    }

    onShowUserPickerChanged: {
        if (showUserPicker && !GreeterState.username)
            applyPickerPreviewTheme();
        if (!showUserPicker)
            userListOpen = false;
    }

    Rectangle {
        anchors.fill: parent
        color: SettingsData.effectiveWallpaperBackgroundColor
    }

    readonly property bool hasCustomWallpaper: SettingsData.lockScreenWallpaperPath !== ""
    readonly property string wallpaperSource: {
        if (hasCustomWallpaper)
            return encodeFileUrl(GreetdSettings.resolveUserPath(SettingsData.lockScreenWallpaperPath));
        var w = SessionData.getMonitorWallpaper(screenName);
        return (w && !w.startsWith("#")) ? encodeFileUrl(w) : "";
    }
    readonly property string wallpaperFillModeName: {
        if (SettingsData.lockScreenWallpaperFillMode !== "")
            return SettingsData.lockScreenWallpaperFillMode;
        return hasCustomWallpaper ? "Fill" : SessionData.getMonitorWallpaperFillMode(screenName);
    }

    DankBackdrop {
        anchors.fill: parent
        screenName: root.screenName
        visible: root.wallpaperSource === "" || wallpaperBackground.status === Image.Error
    }

    Image {
        id: wallpaperBackground

        anchors.fill: parent
        source: root.wallpaperSource
        fillMode: Theme.getFillMode(root.wallpaperFillModeName)
        smooth: true
        asynchronous: false
        cache: true
        visible: source !== ""
        layer.enabled: true

        layer.effect: MultiEffect {
            autoPaddingEnabled: false
            blurEnabled: true
            blur: Theme.lockScreenBlur
            blurMax: Theme.lockScreenBlurMax
            blurMultiplier: 1
        }

        Behavior on opacity {
            NumberAnimation {
                duration: LockMetrics.effectsDuration
                easing.type: Easing.BezierSpline
                easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
            }
        }
    }

    Rectangle {
        anchors.fill: parent
        color: Theme.screenOffColor
        opacity: Theme.lockScreenScrimAlpha
    }

    SystemClock {
        id: systemClock
        precision: SystemClock.Seconds
    }

    Rectangle {
        anchors.fill: parent
        color: "transparent"

        MouseArea {
            anchors.fill: parent
            enabled: root.userListOpen
            visible: root.userListOpen
            onClicked: root.userListOpen = false
        }

        Column {
            id: greeterMainColumn

            anchors.horizontalCenter: parent.horizontalCenter
            anchors.verticalCenter: parent.verticalCenter
            spacing: Theme.spacingM
            width: Math.min(LockMetrics.passwordRowWidth, parent.width - Theme.spacingXL * 2)

            Item {
                id: clockContainer

                width: parent.width
                height: clockText.implicitHeight

                Row {
                    id: clockText

                    anchors.horizontalCenter: parent.horizontalCenter
                    anchors.top: parent.top
                    spacing: 0

                    property string fullTimeStr: {
                        const format = SettingsData.getEffectiveTimeFormat();
                        return systemClock.date.toLocaleTimeString(I18n.locale(), format);
                    }
                    property var timeParts: fullTimeStr.split(':')
                    property string hours: timeParts[0] || ""
                    property string minutes: timeParts[1] || ""
                    property string secondsWithAmPm: timeParts.length > 2 ? timeParts[2] : ""
                    property string seconds: secondsWithAmPm.replace(/\s*(AM|PM|am|pm)$/i, '')
                    property string ampm: {
                        const match = fullTimeStr.match(/\s*(AM|PM|am|pm)$/i);
                        return match ? match[0].trim() : "";
                    }
                    property bool hasSeconds: timeParts.length > 2

                    ClockDigitText {
                        width: clockText.hours.length > 1 ? LockMetrics.clockDigitWidth : 0
                        text: clockText.hours.length > 1 ? clockText.hours[0] : ""
                    }

                    ClockDigitText {
                        width: LockMetrics.clockDigitWidth
                        text: clockText.hours.length > 1 ? clockText.hours[1] : clockText.hours.length > 0 ? clockText.hours[0] : ""
                    }

                    ClockDigitText {
                        text: ":"
                    }

                    ClockDigitText {
                        width: LockMetrics.clockDigitWidth
                        text: clockText.minutes.length > 0 ? clockText.minutes[0] : ""
                    }

                    ClockDigitText {
                        width: LockMetrics.clockDigitWidth
                        text: clockText.minutes.length > 1 ? clockText.minutes[1] : ""
                    }

                    ClockDigitText {
                        text: clockText.hasSeconds ? ":" : ""
                        visible: clockText.hasSeconds
                    }

                    ClockDigitText {
                        width: LockMetrics.clockDigitWidth
                        text: clockText.hasSeconds && clockText.seconds.length > 0 ? clockText.seconds[0] : ""
                        visible: clockText.hasSeconds
                    }

                    ClockDigitText {
                        width: LockMetrics.clockDigitWidth
                        text: clockText.hasSeconds && clockText.seconds.length > 1 ? clockText.seconds[1] : ""
                        visible: clockText.hasSeconds
                    }

                    ClockDigitText {
                        width: Theme.iconSizeSmall
                        text: " "
                        visible: clockText.ampm !== ""
                    }

                    ClockDigitText {
                        text: clockText.ampm
                        visible: clockText.ampm !== ""
                    }
                }
            }

            StyledText {
                id: dateText

                anchors.horizontalCenter: parent.horizontalCenter
                text: systemClock.date.toLocaleDateString(I18n.locale(), SettingsData.lockDateFormat !== "" ? SettingsData.lockDateFormat : Locale.LongFormat)
                font.pixelSize: Theme.fontSizeXLarge
                color: Theme.lockScreenContentColor
            }

            ColumnLayout {
                id: authColumn

                width: parent.width
                spacing: Theme.spacingM

                RowLayout {
                    LayoutMirroring.enabled: I18n.isRtl
                    LayoutMirroring.childrenInherit: true
                    spacing: Theme.spacingM
                    Layout.fillWidth: true

                    Item {
                        Layout.preferredWidth: LockMetrics.avatarSize
                        Layout.preferredHeight: LockMetrics.avatarSize
                        Layout.alignment: Qt.AlignTop
                        visible: SettingsData.lockScreenShowProfileImage || root.pickerAvailable

                        DankCircularImage {
                            anchors.fill: parent
                            imageSource: {
                                const displayUser = GreeterState.username || root.pickerThemeUsername;
                                if (!displayUser)
                                    return "";
                                const cachedPath = GreeterUsersService.profileImagePath(displayUser);
                                if (!cachedPath)
                                    return "";
                                return encodeFileUrl(cachedPath);
                            }
                            fallbackIcon: "material:person"
                        }

                        Rectangle {
                            anchors.fill: parent
                            radius: Theme.fullRadius(width, height)
                            color: "transparent"
                            border.color: Theme.focusRingColor
                            border.width: (avatarPickerArea.containsMouse || root.userListOpen) && !GreeterState.showPasswordInput ? Theme.focusRingWidth : 0
                            visible: root.pickerAvailable
                            Behavior on border.width {
                                NumberAnimation {
                                    duration: LockMetrics.effectsDuration
                                    easing.type: Easing.BezierSpline
                                    easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                                }
                            }
                        }

                        Rectangle {
                            anchors.fill: parent
                            radius: Theme.fullRadius(width, height)
                            color: Theme.withAlpha(Theme.scrimColor, Theme.scrimAlpha)
                            opacity: (root.pickerAvailable && GreeterState.showPasswordInput && avatarPickerArea.containsMouse) ? 1 : 0
                            visible: opacity > 0

                            Behavior on opacity {
                                NumberAnimation {
                                    duration: LockMetrics.effectsDuration
                                    easing.type: Easing.BezierSpline
                                    easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                                }
                            }

                            DankIcon {
                                anchors.centerIn: parent
                                name: "switch_account"
                                size: Theme.iconSize
                                color: Theme.lockScreenContentColor
                            }
                        }

                        MouseArea {
                            id: avatarPickerArea

                            anchors.fill: parent
                            visible: root.pickerAvailable
                            hoverEnabled: true
                            cursorShape: Qt.PointingHandCursor
                            onClicked: {
                                if (GreeterState.showPasswordInput)
                                    root.returnToUserPicker();
                                else if (root.manualUsernameEntry)
                                    root.returnToUserListFromManualEntry();
                                else
                                    root.userListOpen = !root.userListOpen;
                            }
                        }
                    }

                    Rectangle {
                        id: passwordBox

                        property bool showPassword: false
                        property real errorOffset: 0

                        transform: Translate {
                            x: Math.max(-LockMetrics.shakeDistance, Math.min(LockMetrics.shakeDistance, passwordBox.errorOffset))
                        }

                        Layout.fillWidth: true
                        Layout.preferredHeight: root.showUserPicker && root.userListOpen ? Math.max(LockMetrics.fieldHeight, userPicker.implicitHeight + Theme.spacingM * 2) : LockMetrics.fieldHeight

                        clip: true
                        radius: Theme.fullRadius(width, LockMetrics.fieldHeight)
                        color: Theme.cardSurface
                        border.width: inputField.activeFocus ? Math.max(Theme.outlineWidth, Theme.focusRingWidth) : Theme.layerOutlineWidth
                        border.color: inputField.activeFocus ? Theme.focusRingColor : Theme.outlineMedium

                        GreeterUserPicker {
                            id: userPicker

                            anchors.left: parent.left
                            anchors.right: parent.right
                            anchors.verticalCenter: root.userListOpen ? undefined : parent.verticalCenter
                            anchors.top: root.userListOpen ? parent.top : undefined
                            anchors.margins: Theme.spacingM
                            maxExpandedHeight: root.userPickerMaxHeight
                            visible: root.showUserPicker && !GreeterState.showPasswordInput
                            expanded: root.userListOpen
                            autoLoginVisible: root.autoLoginAvailable
                            autoLoginChecked: root.autoLoginOnSuccess
                            manualEntryVisible: true
                            onUserSelected: username => root.selectUser(username)
                            onToggleRequested: root.userListOpen = !root.userListOpen
                            onAutoLoginToggled: root.autoLoginOnSuccess = !root.autoLoginOnSuccess
                            onManualEntryRequested: root.enterManualUsernameEntry()
                        }

                        DankIcon {
                            id: lockIcon

                            anchors.left: parent.left
                            anchors.leftMargin: Theme.spacingM
                            anchors.verticalCenter: parent.verticalCenter
                            name: GreeterState.showPasswordInput ? "lock" : "person"
                            size: Theme.iconSizeSmall
                            color: inputField.activeFocus ? Theme.primary : Theme.surfaceVariantText
                            visible: !root.showUserPicker
                        }

                        TextInput {
                            id: inputField

                            property bool syncingFromState: false

                            anchors.fill: parent
                            anchors.leftMargin: lockIcon.width + Theme.spacingM * 2
                            anchors.rightMargin: {
                                let margin = Theme.spacingM;
                                if (GreeterState.showPasswordInput && revealButton.visible) {
                                    margin += revealButton.width;
                                }
                                if (externalAuthButton.visible) {
                                    margin += externalAuthButton.width;
                                }
                                if (virtualKeyboardButton.visible) {
                                    margin += virtualKeyboardButton.width;
                                }
                                if (enterButton.visible) {
                                    margin += enterButton.width + Theme.spacingXXS;
                                }
                                return margin;
                            }
                            enabled: !root.showUserPicker || GreeterState.showPasswordInput
                            opacity: 0
                            focus: !root.showUserPicker || GreeterState.showPasswordInput
                            echoMode: GreeterState.showPasswordInput ? (parent.showPassword ? TextInput.Normal : TextInput.Password) : TextInput.Normal
                            KeyNavigation.tab: virtualKeyboardButton.visible ? virtualKeyboardButton : sessionDropdown
                            KeyNavigation.backtab: powerButton.visible ? powerButton : sessionDropdown

                            // Contract the on-screen Keyboard drives its target through.
                            function insertText(value) {
                                if (value)
                                    insert(cursorPosition, value);
                            }

                            function backspace() {
                                if (cursorPosition > 0)
                                    remove(cursorPosition - 1, cursorPosition);
                            }

                            onTextChanged: {
                                if (syncingFromState)
                                    return;
                                if (GreeterState.showPasswordInput) {
                                    GreeterState.passwordBuffer = text;
                                    if (!text || text.length === 0)
                                        root.passwordSubmitRequested = false;
                                } else {
                                    GreeterState.usernameInput = text;
                                }
                            }
                            onAccepted: {
                                if (GreeterState.showPasswordInput) {
                                    root.startAuthSession(true);
                                } else {
                                    if (text.trim()) {
                                        root.submitUsername(text);
                                        syncingFromState = true;
                                        text = "";
                                        syncingFromState = false;
                                    }
                                }
                            }

                            Component.onCompleted: {
                                syncingFromState = true;
                                text = GreeterState.showPasswordInput ? GreeterState.passwordBuffer : GreeterState.usernameInput;
                                syncingFromState = false;
                                if (isPrimaryScreen && !powerMenu.isVisible)
                                    forceActiveFocus();
                            }
                            onVisibleChanged: {
                                if (visible && isPrimaryScreen && !powerMenu.isVisible)
                                    forceActiveFocus();
                            }
                        }

                        KeyboardController {
                            id: keyboard_controller
                            target: inputField
                            rootObject: root
                            expressive: true
                        }

                        StyledText {
                            id: placeholder

                            anchors.left: lockIcon.right
                            anchors.leftMargin: Theme.spacingM
                            anchors.right: (GreeterState.showPasswordInput && revealButton.visible ? revealButton.left : (externalAuthButton.visible ? externalAuthButton.left : (virtualKeyboardButton.visible ? virtualKeyboardButton.left : (enterButton.visible ? enterButton.left : parent.right))))
                            anchors.rightMargin: Theme.spacingXXS
                            anchors.verticalCenter: parent.verticalCenter
                            text: {
                                if (GreeterState.unlocking) {
                                    return I18n.tr("Logging in...");
                                }
                                if (Greetd.state !== GreetdState.Inactive && !awaitingExternalAuth && !pendingPasswordResponse) {
                                    return I18n.tr("Authenticating...");
                                }
                                if (GreeterState.showPasswordInput) {
                                    return root.authPromptText !== "" ? root.authPromptText : I18n.tr("Password...");
                                }
                                if (root.showUserPicker) {
                                    return "";
                                }
                                return I18n.tr("Username...");
                            }
                            color: (GreeterState.unlocking || (Greetd.state !== GreetdState.Inactive && !awaitingExternalAuth && !pendingPasswordResponse)) ? Theme.primary : Theme.outline
                            font.pixelSize: Theme.fontSizeMedium
                            opacity: (GreeterState.showPasswordInput ? GreeterState.passwordBuffer.length === 0 : (root.showUserPicker ? false : GreeterState.usernameInput.length === 0)) ? 1 : 0

                            Behavior on opacity {
                                NumberAnimation {
                                    duration: LockMetrics.effectsDuration
                                    easing.type: Easing.BezierSpline
                                    easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                                }
                            }

                            Behavior on color {
                                ColorAnimation {
                                    duration: LockMetrics.effectsDuration
                                    easing.type: Easing.BezierSpline
                                    easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                                }
                            }
                        }

                        StyledText {
                            anchors.left: lockIcon.right
                            anchors.leftMargin: Theme.spacingM
                            anchors.right: (GreeterState.showPasswordInput && revealButton.visible ? revealButton.left : (externalAuthButton.visible ? externalAuthButton.left : (virtualKeyboardButton.visible ? virtualKeyboardButton.left : (enterButton.visible ? enterButton.left : parent.right))))
                            anchors.rightMargin: Theme.spacingXXS
                            anchors.verticalCenter: parent.verticalCenter
                            text: {
                                if (GreeterState.showPasswordInput) {
                                    if (parent.showPassword) {
                                        return GreeterState.passwordBuffer;
                                    }
                                    return "•".repeat(GreeterState.passwordBuffer.length);
                                }
                                return GreeterState.usernameInput;
                            }
                            color: Theme.surfaceText
                            font.pixelSize: (GreeterState.showPasswordInput && !parent.showPassword) ? Theme.fontSizeLarge : Theme.fontSizeMedium
                            opacity: (GreeterState.showPasswordInput ? GreeterState.passwordBuffer.length > 0 : (root.showUserPicker ? false : GreeterState.usernameInput.length > 0)) ? 1 : 0
                            clip: true
                            elide: Text.ElideNone
                            horizontalAlignment: implicitWidth > width ? Text.AlignRight : Text.AlignLeft

                            Behavior on opacity {
                                NumberAnimation {
                                    duration: LockMetrics.effectsDuration
                                    easing.type: Easing.BezierSpline
                                    easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                                }
                            }
                        }

                        LockActionButton {
                            id: revealButton

                            activeFocusOnTab: false
                            Accessible.name: parent.showPassword ? I18n.tr("Hide password") : I18n.tr("Show password")

                            anchors.right: externalAuthButton.visible ? externalAuthButton.left : (virtualKeyboardButton.visible ? virtualKeyboardButton.left : (enterButton.visible ? enterButton.left : parent.right))
                            anchors.rightMargin: 0
                            anchors.verticalCenter: parent.verticalCenter
                            iconName: parent.showPassword ? "visibility_off" : "visibility"
                            buttonSize: Theme.buttonHeightXS
                            visible: GreeterState.showPasswordInput && GreeterState.passwordBuffer.length > 0 && (Greetd.state === GreetdState.Inactive || awaitingExternalAuth || pendingPasswordResponse) && !GreeterState.unlocking
                            enabled: visible
                            onClicked: parent.showPassword = !parent.showPassword
                        }
                        LockActionButton {
                            id: externalAuthButton

                            activeFocusOnTab: false
                            tooltipText: root.greeterPamHasFprint ? I18n.tr("Fingerprint") : I18n.tr("Security key")

                            anchors.right: virtualKeyboardButton.visible ? virtualKeyboardButton.left : (enterButton.visible ? enterButton.left : parent.right)
                            anchors.rightMargin: 0
                            anchors.verticalCenter: parent.verticalCenter
                            iconName: root.greeterPamHasFprint ? "fingerprint" : "key"
                            buttonSize: Theme.buttonHeightXS
                            visible: GreeterState.showPasswordInput && root.greeterExternalAuthAvailable && GreeterState.passwordBuffer.length === 0 && (Greetd.state === GreetdState.Inactive || awaitingExternalAuth || pendingPasswordResponse) && !GreeterState.unlocking
                            enabled: visible
                            onClicked: root.startAuthSession(false)
                        }
                        LockActionButton {
                            id: virtualKeyboardButton

                            Accessible.name: I18n.tr("Keyboard")
                            KeyNavigation.tab: sessionDropdown
                            KeyNavigation.backtab: inputField
                            Keys.onEscapePressed: {
                                keyboard_controller.hide();
                                inputField.forceActiveFocus();
                            }

                            anchors.right: enterButton.visible ? enterButton.left : parent.right
                            anchors.rightMargin: enterButton.visible ? 0 : Theme.spacingS
                            anchors.verticalCenter: parent.verticalCenter
                            iconName: "keyboard"
                            buttonSize: Theme.buttonHeightXS
                            visible: (Greetd.state === GreetdState.Inactive || awaitingExternalAuth || pendingPasswordResponse) && !GreeterState.unlocking && (!root.showUserPicker || GreeterState.showPasswordInput)
                            enabled: visible
                            onClicked: {
                                if (keyboard_controller.isKeyboardActive) {
                                    keyboard_controller.hide();
                                } else {
                                    keyboard_controller.show();
                                }
                            }
                        }

                        LockActionButton {
                            id: enterButton

                            activeFocusOnTab: false
                            Accessible.name: I18n.tr("Login")

                            anchors.right: parent.right
                            anchors.rightMargin: Theme.spacingXXS
                            anchors.verticalCenter: parent.verticalCenter
                            iconName: "keyboard_return"
                            buttonSize: Theme.buttonHeightXS
                            visible: (Greetd.state === GreetdState.Inactive || awaitingExternalAuth || pendingPasswordResponse) && !GreeterState.unlocking && (!root.showUserPicker || GreeterState.showPasswordInput)
                            enabled: true
                            onClicked: {
                                if (GreeterState.showPasswordInput) {
                                    root.startAuthSession(true);
                                } else {
                                    if (inputField.text.trim()) {
                                        root.submitUsername(inputField.text);
                                        inputField.text = "";
                                    }
                                }
                            }

                            Behavior on opacity {
                                NumberAnimation {
                                    duration: LockMetrics.effectsDuration
                                    easing.type: Easing.BezierSpline
                                    easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                                }
                            }
                        }

                        Behavior on border.color {
                            ColorAnimation {
                                duration: LockMetrics.effectsDuration
                                easing.type: Easing.BezierSpline
                                easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                            }
                        }

                        Behavior on Layout.preferredHeight {
                            NumberAnimation {
                                duration: LockMetrics.effectsDuration
                                easing.type: Easing.BezierSpline
                                easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                            }
                        }
                    }
                }

                Item {
                    Layout.fillWidth: true
                    Layout.preferredHeight: root.showAccountSwitchLink ? Theme.buttonHeightXS : 0
                    visible: root.showAccountSwitchLink

                    StyledText {
                        id: accountSwitchLabel

                        anchors.horizontalCenter: parent.horizontalCenter
                        text: I18n.tr("Back to user list", "greeter link to return from manual username entry to user picker")
                        color: Theme.primary
                        font.pixelSize: Theme.fontSizeSmall
                        font.underline: accountSwitchMouse.containsMouse
                    }

                    MouseArea {
                        id: accountSwitchMouse

                        anchors.fill: parent
                        hoverEnabled: true
                        cursorShape: Qt.PointingHandCursor
                        onClicked: root.returnToUserListFromManualEntry()
                    }
                }

                StyledText {
                    Layout.fillWidth: true
                    Layout.preferredHeight: Math.ceil(Theme.fontSizeSmall * 3)
                    Layout.topMargin: -Theme.spacingS
                    Layout.bottomMargin: -Theme.spacingS
                    text: root.authDisplayMessage
                    color: root.authFeedbackMessage !== "" ? Theme.error : Theme.lockScreenContentColor
                    font.pixelSize: Theme.fontSizeSmall
                    horizontalAlignment: Text.AlignHCenter
                    wrapMode: Text.WordWrap
                    maximumLineCount: 2
                    opacity: root.authDisplayMessage !== "" ? 1 : 0

                    Behavior on opacity {
                        NumberAnimation {
                            duration: LockMetrics.effectsDuration
                            easing.type: Easing.BezierSpline
                            easing.bezierCurve: Theme.expressiveCurves.expressiveEffects
                        }
                    }
                }
            }
        }

        GreeterStatusRow {
            anchors.top: parent.top
            anchors.right: parent.right
            anchors.margins: Theme.spacingXL
            showWeather: SettingsData.lockScreenShowWeather
            useFahrenheit: SettingsData.useFahrenheit
            keyboardLayoutVisible: root.keyboardLayoutCount > 1
            keyboardLayoutLabel: root.keyboardLayoutLabel
            onKeyboardLayoutCycleRequested: root.cycleKeyboardLayout()
        }

        LockActionButton {
            id: powerButton

            Accessible.name: I18n.tr("Power Options")
            KeyNavigation.tab: inputField
            KeyNavigation.backtab: sessionDropdown
            anchors.bottom: parent.bottom
            anchors.left: parent.left
            anchors.margins: Theme.spacingXL
            visible: SettingsData.lockScreenShowPowerActions
            iconName: "power_settings_new"
            iconColor: Theme.onSecondaryContainer
            backgroundColor: Theme.secondaryContainer
            radius: pressed ? Theme.cornerRadiusS : Theme.fullRadius(width, height)
            buttonSize: Theme.buttonHeightM
            onClicked: powerMenu.show()
        }

        Item {
            anchors.bottom: parent.bottom
            anchors.right: parent.right
            anchors.margins: Theme.spacingXL
            width: Math.max(Theme.fieldDefaultWidth, currentSessionMetrics.width + Theme.buttonHeightM + Theme.spacingXL)
            height: LockMetrics.fieldHeight

            StyledTextMetrics {
                id: currentSessionMetrics
                text: root.currentSessionName
            }

            property real longestSessionWidth: {
                let maxWidth = 0;
                for (var i = 0; i < sessionMetricsRepeater.count; i++) {
                    const item = sessionMetricsRepeater.itemAt(i);
                    if (item && item.width > maxWidth) {
                        maxWidth = item.width;
                    }
                }
                return maxWidth;
            }

            Repeater {
                id: sessionMetricsRepeater
                model: GreeterState.sessionList
                delegate: StyledTextMetrics {
                    text: modelData
                }
            }

            DankDropdown {
                id: sessionDropdown
                anchors.fill: parent
                focusReturnTarget: inputField
                KeyNavigation.tab: powerButton.visible ? powerButton : inputField
                KeyNavigation.backtab: virtualKeyboardButton.visible ? virtualKeyboardButton : inputField
                text: ""
                description: ""
                backgroundColor: Theme.cardSurface
                hoverBackgroundColor: Theme.blend(Theme.cardSurface, Theme.onSurface, Theme.stateLayerHover)
                normalBorderColor: Theme.outlineMedium
                currentValue: root.currentSessionName
                options: GreeterState.sessionList
                enableFuzzySearch: GreeterState.sessionList.length > 5
                popupWidthOffset: 0
                popupWidth: Math.max(Theme.fieldDefaultWidth + Theme.buttonHeightM, parent.longestSessionWidth + Theme.buttonHeightM + Theme.spacingXL * 2)
                openUpwards: true
                alignPopupRight: true
                onValueChanged: value => {
                    const idx = GreeterState.sessionList.indexOf(value);
                    if (idx < 0)
                        return;
                    GreeterState.sessionManuallySelected = true;
                    GreeterState.currentSessionIndex = idx;
                    GreeterState.selectedSession = GreeterState.sessionExecs[idx];
                    GreeterState.selectedSessionPath = GreeterState.sessionPaths[idx];
                    GreeterState.selectedSessionDesktopId = GreeterState.sessionDesktopIds[idx];
                    GreeterState.selectedSessionDesktopNames = GreeterState.sessionDesktopNames[idx] || "";
                }
            }
        }
    }

    property string currentSessionName: GreeterState.sessionList[GreeterState.currentSessionIndex] || ""

    function finalizeSessionSelection() {
        if (GreeterState.sessionManuallySelected)
            return;
        if (GreeterState.sessionList.length === 0)
            return;
        if (!GreetdMemory.memoryReady)
            return;
        if (!GreetdSettings.settingsLoaded)
            return;

        const savedSession = GreetdSettings.rememberLastSession ? GreetdMemory.lastSessionId : "";
        const savedDesktopId = GreetdSettings.rememberLastSession ? (GreetdMemory.lastSessionDesktopId || desktopIdFromPath(GreetdMemory.lastSessionId)) : "";
        if ((savedSession || savedDesktopId) && GreetdSettings.rememberLastSession) {
            for (var i = 0; i < GreeterState.sessionPaths.length; i++) {
                if ((savedDesktopId && GreeterState.sessionDesktopIds[i] === savedDesktopId) || (savedSession && GreeterState.sessionPaths[i] === savedSession)) {
                    GreeterState.currentSessionIndex = i;
                    GreeterState.selectedSession = GreeterState.sessionExecs[i] || "";
                    GreeterState.selectedSessionPath = GreeterState.sessionPaths[i];
                    GreeterState.selectedSessionDesktopId = GreeterState.sessionDesktopIds[i] || "";
                    GreeterState.selectedSessionDesktopNames = GreeterState.sessionDesktopNames[i] || "";
                    return;
                }
            }
        }

        GreeterState.currentSessionIndex = 0;
        GreeterState.selectedSession = GreeterState.sessionExecs[0] || "";
        GreeterState.selectedSessionPath = GreeterState.sessionPaths[0] || "";
        GreeterState.selectedSessionDesktopId = GreeterState.sessionDesktopIds[0] || "";
        GreeterState.selectedSessionDesktopNames = GreeterState.sessionDesktopNames[0] || "";
    }

    property var sessionDirs: {
        const homeDir = Quickshell.env("HOME") || "";
        const dirs = ["/usr/share/wayland-sessions", "/usr/share/xsessions", "/usr/local/share/wayland-sessions", "/usr/local/share/xsessions"];

        if (homeDir) {
            dirs.push(homeDir + "/.local/share/wayland-sessions");
            dirs.push(homeDir + "/.local/share/xsessions");
        }

        if (xdgDataDirs) {
            xdgDataDirs.split(":").forEach(dir => {
                if (dir) {
                    dirs.push(dir + "/wayland-sessions");
                    dirs.push(dir + "/xsessions");
                }
            });
        }

        // _addSession guards against a session name already existing
        // so we have to load from the user directories first so they
        // correctly override a system configuration
        return dirs.reverse();
    }

    property var _pendingFiles: ({})
    property int _pendingCount: 0

    function _addSession(path, name, exec, desktopNames) {
        if (!name || !exec || GreeterState.sessionList.includes(name))
            return;
        GreeterState.sessionList = GreeterState.sessionList.concat([name]);
        GreeterState.sessionExecs = GreeterState.sessionExecs.concat([exec]);
        GreeterState.sessionPaths = GreeterState.sessionPaths.concat([path]);
        GreeterState.sessionDesktopIds = GreeterState.sessionDesktopIds.concat([desktopIdFromPath(path)]);
        GreeterState.sessionDesktopNames = GreeterState.sessionDesktopNames.concat([desktopNames]);
    }

    function _parseDesktopFile(content, path) {
        let name = "";
        let exec = "";
        let desktopNames = "";
        const lines = content.split("\n");
        for (let i = 0; i < lines.length; i++) {
            const line = lines[i];
            if (!name && line.startsWith("Name="))
                name = line.substring(5).trim();
            else if (!exec && line.startsWith("Exec="))
                exec = line.substring(5).trim();
            else if (!desktopNames && line.startsWith("DesktopNames="))
                desktopNames = line.substring(13).trim();
        }
        _addSession(path, name, exec, desktopNames);
    }

    function sessionLaunchEnv(sessionDesktopId, desktopNames) {
        const env = ["XDG_SESSION_TYPE=wayland"];
        const desktopSession = (sessionDesktopId || "").replace(/\.desktop$/, "");
        if (desktopSession)
            env.push("XDG_SESSION_DESKTOP=" + desktopSession, "DESKTOP_SESSION=" + desktopSession);
        const currentDesktop = (desktopNames || "").replace(/;/g, ":").replace(/^:+|:+$/g, "");
        if (currentDesktop)
            env.push("XDG_CURRENT_DESKTOP=" + currentDesktop);
        return env;
    }

    function _loadDesktopFile(filePath) {
        if (_pendingFiles[filePath])
            return;
        _pendingFiles[filePath] = true;
        _pendingCount++;

        const loader = desktopFileLoader.createObject(root, {
            "filePath": filePath
        });
    }

    function _onFileLoaded(filePath) {
        _pendingCount--;
        if (_pendingCount === 0)
            Qt.callLater(finalizeSessionSelection);
    }

    Component {
        id: desktopFileLoader

        FileView {
            id: fv
            property string filePath: ""
            path: filePath

            onLoaded: {
                root._parseDesktopFile(text(), filePath);
                root._onFileLoaded(filePath);
                fv.destroy();
            }

            onLoadFailed: {
                root._onFileLoaded(filePath);
                fv.destroy();
            }
        }
    }

    Repeater {
        model: isPrimaryScreen ? sessionDirs : []

        Item {
            required property string modelData

            FolderListModel {
                folder: encodeFileUrl(modelData)
                nameFilters: ["*.desktop"]
                showDirs: false
                showDotAndDotDot: false

                onStatusChanged: {
                    if (status !== FolderListModel.Ready)
                        return;
                    for (let i = 0; i < count; i++) {
                        let fp = get(i, "filePath");
                        if (fp.startsWith("file://"))
                            fp = fp.substring(7);
                        root._loadDesktopFile(fp);
                    }
                }
            }
        }
    }

    Connections {
        target: Greetd
        enabled: isPrimaryScreen

        function onAuthMessage(message, error, responseRequired, echoResponse) {
            if (responseRequired) {
                awaitingExternalAuth = false;
                pendingPasswordResponse = true;
                // Show PAM's actual prompt (e.g. "PIN:") instead of a hardcoded "Password...",
                // so custom auth modules (PIN, OTP, smartcard, ...) aren't mislabeled.
                const trimmedPrompt = (message || "").replace(/:\s*$/, "").trim();
                root.authPromptText = trimmedPrompt;
                const hasPasswordBuffer = GreeterState.passwordBuffer && GreeterState.passwordBuffer.length > 0;
                if (!passwordSubmitRequested && hasPasswordBuffer)
                    passwordSubmitRequested = true;
                if (passwordSubmitRequested && !root.submitBufferedPassword())
                    passwordSubmitRequested = false;
                if (passwordSubmitRequested || hasPasswordBuffer) {
                    authTimeout.interval = defaultAuthTimeoutMs;
                    authTimeout.restart();
                } else {
                    authTimeout.stop();
                }
                return;
            }
            pendingPasswordResponse = false;
            if (!passwordSubmitRequested)
                awaitingExternalAuth = root.greeterExternalAuthAvailable;
            if (awaitingExternalAuth || (passwordSubmitRequested && root.greeterPamHasExternalAuth))
                authTimeout.interval = externalAuthTimeoutMs;
            else
                authTimeout.interval = defaultAuthTimeoutMs;
            authTimeout.restart();
            Greetd.respond("");
        }

        function onStateChanged() {
            if (Greetd.state === GreetdState.Inactive) {
                awaitingExternalAuth = false;
                pendingPasswordResponse = false;
                authTimeout.interval = defaultAuthTimeoutMs;
                authTimeout.stop();
                passwordSubmitRequested = false;
            }
        }

        function onReadyToLaunch() {
            awaitingExternalAuth = false;
            pendingPasswordResponse = false;
            passwordSubmitRequested = false;
            authTimeout.interval = defaultAuthTimeoutMs;
            authTimeout.stop();
            passwordFailureCount = 0;
            clearAuthFeedback();
            const sessionCmd = GreeterState.selectedSession || GreeterState.sessionExecs[GreeterState.currentSessionIndex];
            const sessionPath = GreeterState.selectedSessionPath || GreeterState.sessionPaths[GreeterState.currentSessionIndex];
            const sessionDesktopId = GreeterState.selectedSessionDesktopId || GreeterState.sessionDesktopIds[GreeterState.currentSessionIndex] || desktopIdFromPath(sessionPath);
            const sessionDesktopNames = GreeterState.selectedSessionDesktopNames || GreeterState.sessionDesktopNames[GreeterState.currentSessionIndex] || "";
            if (!sessionCmd) {
                GreeterState.pamState = "error";
                authFeedbackMessage = currentAuthMessage();
                placeholderDelay.restart();
                return;
            }

            GreeterState.unlocking = true;
            launchTimeout.restart();
            if (GreetdSettings.rememberLastSession) {
                GreetdMemory.setLastSession(sessionPath, sessionDesktopId);
            } else if (GreetdMemory.lastSessionId || GreetdMemory.lastSessionDesktopId) {
                GreetdMemory.setLastSession("", "");
            }
            if (GreetdSettings.rememberLastUser) {
                GreetdMemory.setLastSuccessfulUser(GreeterState.username);
            } else if (GreetdMemory.lastSuccessfulUser) {
                GreetdMemory.setLastSuccessfulUser("");
            }
            if (root.autoLoginOnSuccess)
                greeterAutoLoginPendingProcess.running = true;
            pendingLaunchCommand = sessionCmd;
            pendingLaunchEnv = sessionLaunchEnv(sessionDesktopId, sessionDesktopNames).concat(["DMS_GREETER_AUTH_TIME=" + Math.floor(Date.now() / 1000)]);
            if (Quickshell.env("DMS_VOID") === "1")
                pendingLaunchEnv.push("LIBSEAT_BACKEND=logind");
            memoryFlushTimer.restart();
        }

        function onAuthFailure(message) {
            awaitingExternalAuth = false;
            pendingPasswordResponse = false;
            passwordSubmitRequested = false;
            // The session that carried the prompt is gone, so allow a fresh one
            // to be started for this user - otherwise the per-user guard keeps
            // the field on its generic label for every attempt after the first.
            authSessionAutoStartedForUser = "";
            authTimeout.interval = defaultAuthTimeoutMs;
            authTimeout.stop();
            launchTimeout.stop();
            GreeterState.unlocking = false;
            if (isLikelyLockoutMessage(message)) {
                GreeterState.pamState = "max";
            } else {
                GreeterState.pamState = "fail";
                passwordFailureCount = passwordFailureCount + 1;
            }
            authFeedbackMessage = currentAuthMessage();
            GreeterState.passwordBuffer = "";
            inputField.text = "";
            placeholderDelay.restart();
            Greetd.cancelSession();
        }

        function onError(error) {
            awaitingExternalAuth = false;
            pendingPasswordResponse = false;
            passwordSubmitRequested = false;
            authSessionAutoStartedForUser = "";
            authTimeout.interval = defaultAuthTimeoutMs;
            authTimeout.stop();
            launchTimeout.stop();
            GreeterState.unlocking = false;
            GreeterState.pamState = "error";
            authFeedbackMessage = currentAuthMessage();
            GreeterState.passwordBuffer = "";
            inputField.text = "";
            placeholderDelay.restart();
            Greetd.cancelSession();
        }
    }

    Timer {
        id: memoryFlushTimer
        interval: memoryFlushDelayMs
        onTriggered: {
            if (!pendingLaunchCommand)
                return;
            const sessionCommand = pendingLaunchCommand;
            const launchEnv = pendingLaunchEnv;
            pendingLaunchCommand = "";
            pendingLaunchEnv = [];
            const sessionArgs = sessionCommand.trim().split(/\s+/);
            const needsVoidDbusSession = Quickshell.env("DMS_VOID") === "1" && !Quickshell.env("DBUS_SESSION_BUS_ADDRESS") && sessionArgs[0] !== "dbus-run-session";
            const launchArgs = needsVoidDbusSession ? ["dbus-run-session"].concat(sessionArgs) : sessionArgs;
            Greetd.launch(launchArgs, launchEnv);
        }
    }

    Timer {
        id: authTimeout
        interval: defaultAuthTimeoutMs
        onTriggered: {
            if (GreeterState.unlocking || Greetd.state === GreetdState.Inactive)
                return;
            awaitingExternalAuth = false;
            pendingPasswordResponse = false;
            passwordSubmitRequested = false;
            authTimeout.interval = defaultAuthTimeoutMs;
            GreeterState.pamState = "error";
            authFeedbackMessage = currentAuthMessage();
            GreeterState.passwordBuffer = "";
            inputField.text = "";
            placeholderDelay.restart();
            Greetd.cancelSession();
        }
    }

    Timer {
        id: launchTimeout
        interval: 8000
        onTriggered: {
            if (!GreeterState.unlocking)
                return;
            pendingPasswordResponse = false;
            passwordSubmitRequested = false;
            GreeterState.unlocking = false;
            GreeterState.pamState = "error";
            authFeedbackMessage = currentAuthMessage();
            placeholderDelay.restart();
            Greetd.cancelSession();
        }
    }

    Timer {
        id: placeholderDelay
        interval: 4000
        onTriggered: clearAuthFeedback()
    }

    SequentialAnimation {
        id: errorShake

        NumberAnimation {
            target: passwordBox
            property: "errorOffset"
            to: LockMetrics.shakeDistance
            duration: LockMetrics.shakeDuration / 3
            easing.type: Easing.BezierSpline
            easing.bezierCurve: Theme.expressiveCurves.expressiveFastSpatial
        }
        NumberAnimation {
            target: passwordBox
            property: "errorOffset"
            to: -LockMetrics.shakeDistance
            duration: LockMetrics.shakeDuration / 3
            easing.type: Easing.BezierSpline
            easing.bezierCurve: Theme.expressiveCurves.expressiveFastSpatial
        }
        NumberAnimation {
            target: passwordBox
            property: "errorOffset"
            to: 0
            duration: LockMetrics.shakeDuration / 3
            easing.type: Easing.BezierSpline
            easing.bezierCurve: Theme.expressiveCurves.expressiveFastSpatial
        }
    }

    LockPowerMenu {
        id: powerMenu
        expressive: true
        showLogout: false
        powerActionConfirmOverride: SettingsData.powerActionConfirm
        powerActionHoldDurationOverride: SettingsData.powerActionHoldDuration
        powerMenuActionsOverride: SettingsData.powerMenuActions
        powerMenuDefaultActionOverride: SettingsData.powerMenuDefaultAction
        powerMenuGridLayoutOverride: SettingsData.powerMenuGridLayout
        requiredActions: ["poweroff"]
        onClosed: {
            if (isPrimaryScreen && inputField && inputField.forceActiveFocus) {
                Qt.callLater(() => inputField.forceActiveFocus());
            }
        }
    }
}
